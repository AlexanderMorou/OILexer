#include                      "Root.oilexer";

/* --------------------------------------- *\
|*              B.2.7: Classes             *|
\* --------------------------------------- */

ConstructorDeclaration                    ::=
    InternConstructorDeclaration            |
    ExternConstructorDeclaration            ;

InternConstructorDeclaration              ::=
    Attributes:Attributes;?
    (
        "public"                            |
        "protected" "internal"?             |
        "internal" "protected"?             |
        "private"
    ):Modifiers;?
    Identifier:Name;
    '(' FormalParameterList:Parameters;? ')'
    ConstructorInitializer:Initializer;?
    Block:Body;                             ;

ConstructorInitializer                    ::=
    ':'
    ("base" | "this"):Target;
	Parametered<DelimitedList<Argument:CascadeArguments;, ','>?>;
//  '(' DelimitedList<Argument:CascadeArguments;, ','>? ')';

ExternConstructorDeclaration              ::=
    Attributes:Attributes;?
    (
        "public" "extern"?                  |
        "extern" "public"?                  |
        "protected" "internal"? "extern"?   |
        "internal" "protected"? "extern"?   |
        "protected" "extern"? "internal"?   |
        "internal" "extern"? "protected"?   |
        "extern" "protected"? "internal"?   |
        "extern" "internal" "protected"?    |
        "private" "extern"?                 |
        "extern" "private"?                 
    ):Modifiers;? Identifier:Name;
    '(' FormalParameterList:Parameters;? ')'
    ConstructorInitializer:Initializer;?
    ';'                                     ;

StaticConstructorDeclaration              ::=
    ExternStaticConstructorDeclaration      |
    InternStaticConstructorDeclaration      ;

ExternStaticConstructorDeclaration        ::=
    Attributes:Attributes;
    ("extern" "static" |
     "static" "extern")
    Identifier:Name;
    '(' ')'
    ';'                                     ;

InternStaticConstructorDeclaration        ::=
    Attributes:Attributes;
    "static"
    Identifier:Name;
    '(' ')'
    Block:Body;                             ;

DestructorDeclaration                    ::=>
    ExternDestructorDeclaration             |
    InternDestructorDeclaration             ;

ExternDestructorDeclaration               ::=
    Attributes:Attributes;?
    "extern" '~'Identifier:Name; '(' ')' ';';

InternDestructorDeclaration               ::=
    Attributes:Attributes;?
    '~'Identifier:Name; '(' ')'
    Block:Body;                             ;
