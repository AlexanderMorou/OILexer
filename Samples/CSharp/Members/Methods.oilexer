#include                      "Root.oilexer";

/* --------------------------------------- *\
|*              B.2.7: Classes             *|
\* --------------------------------------- */

MethodDeclaration                        ::=>
    AbstractMethodDeclaration               |
    ConcreteMethodDeclaration               ;

AbstractMethodDeclaration                 ::=
    Attributes:Attributes;?
    SetInject<(
        "new"                               |
        "public"                            |
        "protected"                         |
        "internal"
    ):Modifiers;, "abstract">
    ReturnType:ReturnType;
    Identifier:Name;
    GenericHeader<'(' FormalParameterList:Parameters;? ')'>
    ';'                                     ;

ConcreteMethodDeclaration                ::=>
    InstanceMethodDeclaration               |
    StaticMethodDeclaration                 ;

InstanceMethodDeclaration                 ::=
    Attributes:Attributes;?
    (
        "new"                               |
        "public"                            |
        "protected"                         |
        "internal"                          |
        "private"                           |
        "virtual"                           |
        "sealed"                            |
        "override"                          |
        "extern"
    ):Modifiers;*
    "partial"!:IsPartial;?
    ReturnType:ReturnType;
    MemberName:Name;
    GenericHeader<'(' FormalParameterList:Parameters;? ')'>
    Block:Body;                             ;

StaticMethodDeclaration                   ::=
    Attributes:Attributes;?
    SetInject<(
        "new"                               |
        "public"                            |
        "protected"                         |
        "internal"                          |
        "private"                           |
        "extern"
    ):Modifiers;, "static">
    "partial"!:IsPartial;?
    ReturnType:ReturnType;
    MemberName:Name;
    GenericHeader<'(' MethodFormalParameterList:Parameters;? ')'>
    Block:Body;                             ;

/* --------------------------------------- *\
|*            B.2.10: Interfaces           *|
\* --------------------------------------- */

InterfaceMethodDeclaration                ::=
    Attributes:Attributes;?
    "new"!:IsNew;?
    ReturnType:ReturnType;
    Identifier:Name;
    GenericHeader<'(' FormalParameterList:Parameters;? ')'>
    ';'                                     ;
