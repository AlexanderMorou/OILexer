#ifndef Lexer
#include                        "Oilexer.oilexer";
InvalidAssociation = "Invalid association '{0}'.  Expected: Left or Right", 0x100;

PrecedenceAssociation := @"Left":Left; | @"Right":Right;;
/* BinaryOperationExpression
    <
        Current:Expect=Rule;, 
        Operators:Expect=Token;, 
        Lower:Expect=Rule;, 
        Associativity:Expect=PrecedenceAssociation;
    > ::=
#if Associativity == Left
    #return (Current:Left; Operators)? Lower:Right;;
#elif Associativity == Right
    #return Lower:Left; (Operators Current:Right;)?;
#else
    #throw InvalidAssociation, Associativity;
#endif; */
#elif Lexer == "ToyParser"
#include                       "Language.oilexer";
#endif
BinaryOperationExpression
    <
        Current:Expect=Rule;, 
        Operators:Expect=Token;, 
        Lower:Expect=Rule;, 
        Companion:Expect=Rule;,
        Associativity:Expect=PrecedenceAssociation;
    >                                                         ::=
#if Associativity == Left
    #define Companion = Current:Left; Operators Lower:Right;;
    #return Companion | Lower;
#elif Associativity == Right
    #define Companion = Lower:Left; Operators Current:Right;;
    #return Lower | Companion;
#else
    #throw InvalidAssociation, Associativity;
#endif                                                          ;


ProductionRuleTemplateEntry                                          ::=
    Identifier:Name;
    LanguageOperatorToken.TemplateStart 
        ProductionRuleTemplateArgument:Arguments;
        (
            LanguageOperatorToken.PartSeparator
            ProductionRuleTemplateArgument:Arguments;
        )*
    LanguageOperatorToken.TemplateEnd
    LanguageOperatorToken.RuleStart
    ProductionRuleTemplateSeries:Items; LanguageOperatorToken.Terminal ;

ProductionRuleTemplateArgument                                       ::=
    Identifier:Name; 
    (
        LanguageOperatorToken.OptionsSeparator
        RuleTerminals.TemplateRuleExpect /*@expect*/
        CommonTerminals.Assignment /*=*/
        ProductionRuleTemplateArgumentExpect:Expected;
        LanguageOperatorToken.Terminal
    )?
    LanguageOperatorToken.OneOrMore!:Multiple;?                        ;

ProductionRuleTemplateSeries                                         ::=
    ProductionRuleTemplate:Items;
        (
            LanguageOperatorToken.ExpressionSeparator
            ProductionRuleTemplate:Items;
        )*                                                             ;

ProductionRuleTemplate                                               ::=
    ProductionRuleTemplateItem:Items;*                                 ;

ProductionRuleTemplateItem                                          ::=>
    LiteralProductionRuleItem                                          |
    TemplateReferenceTemplateProductionRuleItem                        |
    SoftReferenceScannableEntryItem                                    |
    GroupProductionRuleTemplateItem                                    |
    PreprocessorProductionRuleTemplateItem                             ;


TemplateReferenceTemplateProductionRuleItem                          ::=
    Identifier:Name; 
        LanguageOperatorToken.TemplateStart /*<*/
            (
                ProductionRuleTemplateSeries:Arguments;
                (
                    LanguageOperatorToken.PartSeparator /*,*/
                    ProductionRuleTemplateSeries:Arguments;
                )*
            )?
        LanguageOperatorToken.TemplateEnd /*>*/
        ScannableEntryItemOptions<>?;



PreprocessorProductionRuleTemplateItem                              ::=>
    PreprocessorIfDirectiveProductionRuleTemplateItem                  |
    TemplatePreprocessorEntry                                          ;

PreprocessorIfDirectiveProductionRuleTemplateItem                   ::=>
    PreprocessorIfConditionDirectiveProductionRuleTemplateItem         |
    PreprocessorIfDefinedDirectiveProductionRuleTemplateItem           |
    PreprocessorIfNotDefinedDirectiveProductionRuleTemplateItem        ;

PreprocessorIfConditionDirectiveProductionRuleTemplateItem           ::=
    PreprocessorType.IfDirective
        PreprocessorIfConditionLogicalOrExp:Condition;
    ProductionRuleTemplateSeries:Body;
    (PreprocessorElseDirectiveProductionRuleTemplateItem:Next;)?
    PreprocessorType.EndIfDirective                                    ;

PreprocessorIfDefinedDirectiveProductionRuleTemplateItem             ::=
    PreprocessorType.IfDefinedDirective
        PreprocessorIfDefConditionLogicalOrExp:Condition;
    ProductionRuleTemplateSeries:Body;
    (PreprocessorElseDirectiveProductionRuleTemplateItem:Next;)?
    PreprocessorType.EndIfDirective                             ;

PreprocessorIfNotDefinedDirectiveProductionRuleTemplateItem          ::=
    PreprocessorType.IfNotDefinedDirective
        PreprocessorIfDefConditionLogicalOrExp:Condition;
    ProductionRuleTemplateSeries:Body;
    (PreprocessorElseDirectiveProductionRuleTemplateItem:Next;)?
    PreprocessorType.EndIfDirective                                    ;

PreprocessorElseDirectiveProductionRuleTemplateItem                 ::=>
    PreprocessorElseIfConditionDirectiveProductionRuleTemplateItem     |
    PreprocessorElseIfDefinedDirectiveProductionRuleTemplateItem       |
    PreprocessorElseIfNotDefinedDirectiveProductionRuleTemplateItem    |
    PreprocessorFinalElseDirectiveProductionRuleTemplateItem           ;

PreprocessorFinalElseDirectiveProductionRuleTemplateItem             ::=
    PreprocessorType.ElseDirective
        ProductionRuleTemplateSeries:Body;                             ;

PreprocessorElseIfConditionDirectiveProductionRuleTemplateItem       ::=
    PreprocessorType.ElseIfDirective
        PreprocessorIfConditionLogicalOrExp:Condition;
    ProductionRuleTemplateSeries:Body;
    (PreprocessorElseDirectiveProductionRuleTemplateItem:Next;)?;

PreprocessorElseIfDefinedDirectiveProductionRuleTemplateItem         ::=
    PreprocessorType.ElseIfDefinedDirective
        PreprocessorIfDefConditionLogicalOrExp:Condition;
    ProductionRuleTemplateSeries:Body;
    (PreprocessorElseDirectiveProductionRuleTemplateItem:Next;)?;

PreprocessorElseIfNotDefinedDirectiveProductionRuleTemplateItem      ::=
    PreprocessorType.ElseIfNotDefinedDirective
        PreprocessorIfDefConditionLogicalOrExp:Condition;
    ProductionRuleTemplateSeries:Body;
    (PreprocessorElseDirectiveProductionRuleTemplateItem:Next;)?;

GroupProductionRuleTemplateItem                                      ::=
    LanguageOperatorToken.BeginGroup /*(*/
        ProductionRuleTemplateSeries:Items;?
    LanguageOperatorToken.EndGroup   /*)*/
    ScannableEntryItemOptions<>?                                       ;
