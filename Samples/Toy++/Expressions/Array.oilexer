#include "Expressions.oilexer";

ArrayCreationExpression                  ::=>
    ArraySizeCreationExpression             |
    ArrayInitializerCreationExpression      ;
//    ArrayRankInitializerCreationExpression;
    
ArraySizeCreationExpression               ::=
    "new" Whitespace NonArrayType:Type;
    Whitespace?
    BracketExpressionList:SizeInitializers;
    Whitespace?
    (Whitespace? RankSpecifier:Ranks;)* 
    Whitespace? 
    ArrayInitializer:Initializer;           ;

ArrayInitializerCreationExpression        ::=
    "new" Whitespace ArrayType:Type;
    ArrayInitializer:Initializer;           ;

RankSpecifier                             ::=
    '[' ','#:Rank;* ']'                     ;

ArrayType                                 ::=
    NonArrayType:ElementType; 
        RankSpecifier:Ranks;+               ;

NonArrayType                             ::=>
    ValueType                               |
    NonArrayReferenceType                   |
    TypeParameter                           |
/* B.3. Grammar extensions for unsafe code */
#ifdef UNSAFECONTEXT
    PointerType
#endif                                      ;

NonArrayReferenceType                    ::=>
    ClassType                               |
    InterfaceOrDelegateType                 ;

ArrayInitializer                         ::=>
    '{'
        Whitespace?
        (
            WhitespaceDelimitedList<
                VariableInitializer:Values;, ','>
            WhitePaddingOpt<','>?
        )?
    '}';

VariableInitializer                      ::=>
    Expression                              |
    ArrayInitializer                        ;

