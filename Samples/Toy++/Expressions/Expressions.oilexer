#include                 "..\\Toy++.oilexer";
#include      "AnonymousExpressions.oilexer";
#include                     "Array.oilexer";
#include                      "Math.oilexer";
#include                      "LINQ.oilexer";
/* B.2.4: Expressions */
/* Grammar rewrite begins with expressions */
ArgumentList<Left, Right>                 ::=
    Left DelimitedList<Argument:Items;, ','>?
    Right                                   ;

Argument                                  ::=
    (
        "ref"                               | 
        "out"
    ):Direction;? Whitespace
    ArgumentExpression:Value;               ;

ArgumentExpression                       ::=>
    Expression                              |
    NamedExpression                         ;

NamedExpression                           ::=
    Identifier:ParameterName; Whitespace
    Expression:NamedExpression;             ;

ExpressionList                            ::=
    WhitespaceDelimitedList<
        Expression:Items;, ','>             ;

EnclosedExpressionList<Left, Right>       ::=
    Left 
        WhitePaddingOpt<ExpressionList:Items;>? 
    Right                                   ;

PrimaryExpression                        ::=>
    ArrayCreationExpression                 |
    PrimaryNoArrayCreationExpression        ;

PrimaryNoArrayCreationExpression         ::=>
    Literal                                 |
    SimpleName                              |
    ParenthesizedExpression                 |
    MemberAccess                            |
    InvocationExpression                    |
    ElementAccess                           |
    ThisAccess                              |
    BaseAccess                              |
    PostcrementExpression                   |
    ObjectCreationExpression                |
    DelegateCreationExpression              |
    AnonymousObjectCreationExpression       |
    TypeOfExpression                        |
    CheckedExpression                       |
    UncheckedExpression                     |
    DefaultValueExpression                  |
    AnonymousMethodExpression               ;

SimpleName ::=
    Identifier:Name; TypeArgumentList:TypeParameters;?;

ParenthesizedExpression ::=
    '(' WhitePaddingOpt<Expression:Inner;> ')';

PrimaryMemberAccess                       ::=
    PrimaryExpression:PreviousAccess; 
    WhitePaddingOpt<'.'>
    SimpleName:CurrentAccess;               ;

PredefinedTypeMemberAccess                ::=
    (
        "bool"                              |
        "byte"                              |
        "char"                              |
        "decimal"                           |
        "double"                            |
        "float"                             |
        "int"                               |
        "long"                              |
        "object"                            |
        "sbyte"                             |
        "short"                             |
        "string"                            |
        "uint"                              |
        "ulong"                             |
        "ushort"
    ):PreviousAccess; 
    WhitePaddingOpt<'.'>     
    SimpleName:CurrentAccess;               ;

QualifiedAliasMemberAccess                ::=
    QualifiedAliasMember:PreviousAccess; '.' 
    Identifier:CurrentAccess;               ;

MemberAccess                             ::=>
    PrimaryMemberAccess                     |
    PredefinedTypeMemberAccess              |
    QualifiedAliasMemberAccess              ;

InvocationExpression                      ::=
    PrimaryExpression:CallSite;
    ArgumentList<'(', ')'>                  ;

ElementAccess                             ::=
   PrimaryNoArrayCreationExpression:CallSite; 
   EnclosedExpressionList<'[', ']'>         ;

ThisAccess                                ::=
    "this"                                  ;

BaseAccess                               ::=>
    BaseMemberAccess                        |
    BaseElementAccess                       ;

BaseMemberAccess                          ::=
    "base" WhitePaddingOpt<'.'>
    Identifier:Target;                      ;

BaseElementAccess                         ::=
    "base" Whitespace?
    EnclosedExpressionList<'[', ']'>        ;

ObjectCreationExpression                  ::=
  "new" Whitespace Type WhitePaddingOpt<ArgumentList<'(', ')'>?>
  Whitespace? ObjectOrCollectionInitializer:Initializer;;

ObjectOrCollectionInitializer            ::=>
    CollectionInitializer                   |
    ObjectInitializer                       ;

ObjectInitializer                         ::=
    '{' 
        WhitePaddingOpt<
            WhitespaceDelimitedList<MemberInitializer:Members;, ','>
            (Whitespace? ',')?>?
    '}'                                     ;

MemberInitializer                         ::=
    Identifier:Target; '=' 
    InitializerValue:Value;                 ;

InitializerValue                         ::=>
    Expression                              |
    ObjectOrCollectionInitializer           ;

CollectionInitializer                     ::=
    '{'
        (
            DelimitedList<ElementInitializer:Elements;, ','>
            ','?
        )?
    '}';

ElementInitializer                       ::=>
    NonAssignmentExpression                 |
    CurlyExpressionList                     ;
    
CurlyExpressionList                       ::=
    EnclosedExpressionList<'{', '}'>        ;

BracketExpressionList                     ::=
    EnclosedExpressionList<'[', ']'>        ;

DelegateCreationExpression                ::=
    "new" InterfaceOrDelegateType:Type;
    '(' Expression:MethodReference; ')'     ;

TypeOfExpression                          ::=
    "typeof"  '(' TypeOfType:Type; ')'      ;

TypeOfType                               ::=>
    Type                                    |
    VoidType                                |
    UnboundTypeName                         ;

UnboundTypeName                           ::=
    (Identifier:Alias; WhitePaddingOpt<"::">)? 
    WhitespaceDelimitedList<UnboundGenericPart:Parts;, '.'>;

UnboundGenericPart                        ::=
    Identifier:Names; Whitespace?
    GenericDimensionSpecifier:Dimensions;?  ;

GenericDimensionSpecifier                 ::=
    '<' (','#:Commas; Whitespace)* '>'      ;

CheckedExpression                         ::=
    "checked" Whitespace? 
    '(' 
        WhitePaddingOpt<Expression:Target;> 
    ')'                                     ;

UncheckedExpression                       ::=
    "unchecked" Whitespace? 
    '(' 
        WhitePaddingOpt<Expression:Target;> 
    ')'                                     ;

DefaultValueExpression                    ::=
    "default" Whitespace?
    '(' 
        WhitePaddingOpt<Type:Target;>
    ')'                                     ;

Expression                               ::=>
    NonAssignmentExpression                 |
    AssignmentExpression                    ;

ConstantExpression                       ::=>
    Expression                              ;

BooleanExpression                        ::=>
    Expression                              ;
