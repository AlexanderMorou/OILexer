#include                   "Members.oilexer";

/* --------------------------------------- *\
|*              B.2.7: Classes             *|
\* --------------------------------------- */

ConstructorDeclaration                   ::=>
    InternConstructorDeclaration            |
    ExternConstructorDeclaration            ;

InternConstructorDeclaration              ::=
    Attributes:Attributes;?
    (
        "public"?                           |
        "private"?                          |
        OTuple<"internal", "protected">
    ):Modifiers;
    Identifier:Name;
    '(' FormalParameterList:Parameters;? ')'
    ConstructorInitializer:Initializer;?
    Block:Body;                             ;

ConstructorInitializer                    ::=
    ':'
    ("base" | "this"):Target;
	Parametered<DelimitedList<Argument:CascadeArguments;, ','>?>;

ExternConstructorDeclaration              ::=
    Attributes:Attributes;?
    (
        Tuple<("public" | "private")?, "extern"> |
        Tuple<"internal"?, "protected"?, "extern">
    ):Modifiers; Identifier:Name;
    '(' FormalParameterList:Parameters;? ')'
    ConstructorInitializer:Initializer;?
    ';'                                     ;

TypeParameterConstructorDeclaration       ::=
    Attributes:Attributes;?
    Identifier:Name;
    '(' FormalParameterList:Parameters;? ')'
    ';'                                     ;

StaticConstructorDeclaration             ::=>
    ExternStaticConstructorDeclaration      |
    InternStaticConstructorDeclaration      ;

ExternStaticConstructorDeclaration        ::=
    Attributes:Attributes;
    Tuple<"extern", "static">
    Identifier:Name;
    '(' ')' ';'                             ;

InternStaticConstructorDeclaration        ::=
    Attributes:Attributes;
    "static"
    Identifier:Name;
    '(' ')'
    Block:Body;                             ;

DestructorDeclaration                    ::=>
    ExternDestructorDeclaration             |
    InternDestructorDeclaration             ;

ExternDestructorDeclaration               ::=
    Attributes:Attributes;?
    "extern" '~'Identifier:Name; '(' ')' ';';

InternDestructorDeclaration               ::=
    Attributes:Attributes;?
    '~'Identifier:Name; '(' ')'
    Block:Body;                             ;

