#include                   "Members.oilexer";

/* --------------------------------------- *\
|*            B.2.10: Interfaces           *|
\* --------------------------------------- */

InterfacePropertyDeclaration              ::=
    Attributes:Attributes;
    "new"!:IsNew;?
    Type:PropertyType;
    Identifier:Name;
    '{'
        TupleR1<
            AbstractGet:GetPart;, 
            AbstractSet:SetPart;>
    '}';

TypeParameterPropertyDeclaration          ::=
    Attributes:Attributes;
    Type:PropertyType;
    Identifier:Name;
    '{'
        TupleR1<
            AbstractGet:GetPart;, 
            AbstractSet:SetPart;>
    '}';
/* --------------------------------------- *\
|*              B.2.7: Classes             *|
\* --------------------------------------- */

PropertyDeclaration                      ::=>
    ConcretePropertyDeclaration             |
    AbstractPropertyDeclaration             ;

ConcretePropertyDeclaration              ::=>
    Attributes:Attributes;?
    (
        Tuple<"override", ("public" | "private")?, "sealed"?,"extern"?> |
        Tuple<"override", "internal"?, "protected"?, "sealed"?, "extern"?> |
        OTuple<"new", ("public" | "private"), "static", "extern"> |
        OTuple<"new", ("public" | "private"), "virtual", "extern"> |
        OTuple<"new", "internal", "protected", "static", "extern"> |
        OTuple<"new", "internal", "protected", "virtual", "extern">
    ):Modifiers;
    Type:PropertyType;
    MemberName:Name;
    '{'
        // (A1 B1? | B1 A1? | A2 B2? | B2 A2?)
        TupleR1Alt<
            ConcreteGet:GetPart;, 
            ConcreteSet:SetPart;, 
            AbstractGet:GetPart;, 
            AbstractSet:SetPart;>
    '}'                                     ;

AbstractPropertyDeclaration               ::=
    Attributes:Attributes;?
    /* *
     * 144 variations
     * */
    ( 
        Tuple<"abstract", "new"?, ("public"? | "private"?), "extern"?> |
        Tuple<"abstract", "new"?, "internal"?, "protected"?, "extern"?>
    ):Modifiers;
    Type:PropertyType;
    Identifier:Name;
    '{'
        TupleR1<AbstractGet:GetPart;, AbstractSet:SetPart;>
    '}';
