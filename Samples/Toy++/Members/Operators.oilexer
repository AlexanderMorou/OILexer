#include                   "Members.oilexer";

/* --------------------------------------- *\
|*              B.2.7: Classes             *|
\* --------------------------------------- */

DeclareInternExtern<
    Intern:Expect=Rule;, 
    Extern:Expect=Rule;, 
    Body> ::=
    #ifdef Extern
        #return 
            Attributes:Attributes;?
            Tuple<"public", "static", "extern">
            Whitespace? Body';'              ;
    #else
        #define Extern = 
            Attributes:Attributes;?
            Tuple<"public", "static", "extern">
            Whitespace? Body ';'            ;
    #endif
    #ifdef Intern
        #return 
            Attributes:Attributes;? Whitespace?
            Tuple<"public", "static"> Whitespace?
            Body  Whitespace? Block:Body;   ;
    #else
        #define Intern = 
            Attributes:Attributes;?
            Tuple<"public", "static">
            Body  Whitespace? Block:Body;   ;
    #endif                                  ;

OperatorDeclaration                      ::=>
    ExternOperatorDeclaration               |
    InternOperatorDeclaration               ;

ExternOperatorDeclaration                ::=>
    ExternBinaryOperatorDeclaration         |
    ExternTypeOperatorDeclaration           |
    ExternUnaryOperatorDeclaration          ;

InternOperatorDeclaration                ::=>
    InternBinaryOperatorDeclaration         |
    InternTypeOperatorDeclaration           |
    InternUnaryOperatorDeclaration          ;

ExternBinaryOperatorDeclaration           ::=
    DeclareInternExtern<
        InternBinaryOperatorDeclaration,
        ExternBinaryOperatorDeclaration,
        Type:OperatorType;
            "operator"
        (
            '+'                             |
            '-'                             |
            '*'                             |
            '/'                             |
            '%'                             |
            '&'                             |
            '|'                             |
            '^'                             |
            "<<"                            |
            ">>"                            |
            "=="                            |
            "!="                            |
            '>'                             |
            '<'                             |
            "<="                            |
            ">="
        ):Operator;
        '(' 
            TypedName:Left; 
            WhitePaddingOpt<','>
            TypedName:Right;
        ')'>                                ;

ExternUnaryOperatorDeclaration            ::=
    DeclareInternExtern<
        InternUnaryOperatorDeclaration,
        ExternUnaryOperatorDeclaration,
        Type:OperatorType;
            "operator" Whitespace
        (
            '+'                             |
            '-'                             |
            '!'                             |
            '~'                             |
            "++"                            |
            "--"                            |
            "true"                          |
            "false"
        ):Operator; Whitespace?
        '('
            WhitePaddingOpt<TypedName:Operand;>
        ')'>                                ;

ExternTypeOperatorDeclaration             ::=
    DeclareInternExtern<
        InternTypeOperatorDeclaration,
        ExternTypeOperatorDeclaration,
        ("implicit" | "explicit"):Operator; Whitespace
        "operator" Whitespace
        Type:OperatorType; Whitespace?
        '(' 
            WhitePaddingOpt<TypedName:Operand;>
        ')'>                                ;
