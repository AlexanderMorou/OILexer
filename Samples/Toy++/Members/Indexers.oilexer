#include                   "Members.oilexer";

/* --------------------------------------- *\
|*            B.2.10: Interfaces           *|
\* --------------------------------------- */

InterfaceIndexerDeclaration               ::=
    Attributes:Attributes;
    "new"!:IsNew;?
    Type:IndexerType;
    (Identifier:Name; | "this")
    '[' FormalParameterList:Parameters;? ']'
    '{'
        TupleR1<
            AbstractGet:GetPart;,
            AbstractSet:SetPart;>
    '}'                                     ;


TypeParameterIndexerDeclaration           ::=
    Attributes:Attributes;
    Type:IndexerType;
    (Identifier:Name; | "this")
    '[' FormalParameterList:Parameters;? ']'
    '{'
        TupleR1<
            AbstractGet:GetPart;,
            AbstractSet:SetPart;>
    '}'                                     ;

/* --------------------------------------- *\
|*              B.2.7: Classes             *|
\* --------------------------------------- */

IndexerDeclaration                       ::=>
    ConcreteIndexerDeclaration              |
    AbstractIndexerDeclaration              ;

ConcreteIndexerDeclaration               ::=>
          ExternInterfaceIndexerDeclaration |
          InternInterfaceIndexerDeclaration |
                   ExternIndexerDeclaration |
                   InternIndexerDeclaration ;

ExternInterfaceIndexerDeclaration ::=
    Attributes:Attributes;?
    "extern"
    Type:IndexerType;
    Type:InterfaceType;
    '.'
    (Identifier | "this"):Name;
    '[' FormalParameterList:Parameters; ']'
    '{'
        TupleR1<
            AbstractGet:GetPart;,
            AbstractSet:SetPart;>
    '}'                                     ;

InternInterfaceIndexerDeclaration ::=
    Attributes:Attributes;?
    Type:IndexerType;
    Type:InterfaceType;
    '.'
    (Identifier | "this"):Name;
    '[' FormalParameterList:Parameters; ']'
    '{'
        TupleR1<
            ConcreteGet:GetPart;,
            ConcreteSet:SetPart;>
    '}'                                     ;


ExternIndexerDeclaration    ::=
    Attributes:Attributes;?
    /* *
     * 288 Variations.
     * */
    (
        Tuple<"override", ("public" | "private")?, "sealed"?,"extern"> |
        Tuple<"override", "internal"?, "protected"?, "sealed"?, "extern"> |
        Tuple<"new"?, ("public" | "private")?, ("static" | "virtual")?, "extern"> |
        Tuple<"new"?, "internal", "protected"?, ("static" | "virtual")?, "extern">
    ):Modifiers;
    Type:IndexerType;
    (Identifier | "this"):Name;
    '[' FormalParameterList:Parameters; ']'
    '{'
        TupleR1<
            AbstractGet:GetPart;,
            AbstractSet:SetPart;>
    '}'                                     ;



InternIndexerDeclaration    ::=
    Attributes:Attributes;?
    /* *
     * 90 Variations.
     * */
    (
        Tuple<"override", ("public" | "private")?, "sealed"?> |
        Tuple<"override", "internal"?, "protected"?, "sealed"?> |
        OTuple<"new", ("public" | "private"), ("static" | "virtual")> |
        OTuple<"new", "internal", "protected", ("static" | "virtual")>
    ):Modifiers;
    Type:IndexerType;
    (Identifier | "this"):Name;
    '[' FormalParameterList:Parameters; ']'
    '{'
        TupleR1<
            ConcreteGet:GetPart;,
            ConcreteSet:SetPart;>
    '}'                                     ;

AbstractIndexerDeclaration                ::=
    Attributes:Attributes;?
    /* *
     * 30 Variations.
     * */
    (
        Tuple<"new"?, ("public" | "private")?, "abstract"> |
        Tuple<"new"?, "protected"?, "internal"?, "abstract">
    ):Modifiers;
    Type:IndexerType;
    (Identifier:Name; | "this")
    '[' FormalParameterList:Parameters; ']'
    '{'
        TupleR1<
            AbstractGet:GetPart;,
            AbstractSet:SetPart;>
    '}'                                     ;
